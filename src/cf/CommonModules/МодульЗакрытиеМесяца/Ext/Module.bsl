
Процедура УстановитьАктуальностьРасчетаСебестоимостиПоПроводимомуДокументу(ДатаДокумента) Экспорт
	_Запрос = Новый Запрос;
	_Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗакрытиеМесяца.Ссылка
		|ИЗ
		|	Документ.ЗакрытиеМесяца КАК ЗакрытиеМесяца
		|ГДЕ
		|	НАЧАЛОПЕРИОДА(ЗакрытиеМесяца.Дата, МЕСЯЦ) >= &НачалоМесяцаТекущаяДата
		|	И ЗакрытиеМесяца.Проведен";

	_Запрос.УстановитьПараметр("НачалоМесяцаТекущаяДата", НачалоМесяца(ДатаДокумента));

	Выборка = _Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ДокументОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
	КонецЦикла;

	_Запрос.Текст =
		"ВЫБРАТЬ
		|	МАКСИМУМ(НАЧАЛОПЕРИОДА(ЗакрытиеМесяца.Дата, МЕСЯЦ)) КАК Дата
		|ИЗ
		|	Документ.ЗакрытиеМесяца КАК ЗакрытиеМесяца
		|ГДЕ
		|	ЗакрытиеМесяца.Проведен
		|
		|ИМЕЮЩИЕ
		|	НЕ МАКСИМУМ(НАЧАЛОПЕРИОДА(ЗакрытиеМесяца.Дата, МЕСЯЦ)) ЕСТЬ NULL";

	Выборка = _Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Константы.КрайняяДатаЗакрытогоПериода.Установить(Выборка.Дата);
	Иначе
		Константы.КрайняяДатаЗакрытогоПериода.Установить('00010101');
	КонецЕсли;
КонецПроцедуры

Процедура ЗакрытьМесяц(ДатаЗакрытия) Экспорт
	СтатусЗакрытияПредыдущегоМесяца = ПолучитьСтатусЗакрытияМесяца(ДобавитьМесяц(ДатаЗакрытия, -1));
	Если НЕ СтатусЗакрытияПредыдущегоМесяца Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Невозможно закрыть текущий месяц, так как есть незакрытые предыдущие месяцы";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;

	_Запрос = Новый Запрос;
	_Запрос.Текст =
		"ВЫБРАТЬ
		|	ПартииТоваровОбороты.Партия.Дата КАК ДатаПартии,
		|	ПартииТоваровОбороты.Номенклатура КАК Номенклатура,
		|	МАКСИМУМ(ПокупкаТовары.Цена) КАК Цена
		|ИЗ
		|	РегистрНакопления.ПартииТоваров.Обороты(&ДатаНачала, &ДатаОкончания, , ) КАК ПартииТоваровОбороты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Покупка.Товары КАК ПокупкаТовары
		|		ПО ПартииТоваровОбороты.Партия = ПокупкаТовары.Ссылка
		|			И ПартииТоваровОбороты.Номенклатура = ПокупкаТовары.Номенклатура
		|ГДЕ
		|	ПартииТоваровОбороты.КоличествоПриход > 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ПартииТоваровОбороты.Номенклатура,
		|	ПартииТоваровОбороты.Партия,
		|	ПартииТоваровОбороты.Партия.Дата";

	_Запрос.УстановитьПараметр("ДатаНачала", НачалоМесяца(ДатаЗакрытия));
	_Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(ДатаЗакрытия));

	ВыборкаСебестоимость = _Запрос.Выполнить().Выбрать();

	_Запрос.Параметры.Очистить();
	_Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗакрытиеМесяца.Ссылка
		|ИЗ
		|	Документ.ЗакрытиеМесяца КАК ЗакрытиеМесяца
		|ГДЕ
		|	НАЧАЛОПЕРИОДА(ЗакрытиеМесяца.Дата, МЕСЯЦ) = &Дата
		|	И НЕ ЗакрытиеМесяца.ПометкаУдаления";

	_Запрос.УстановитьПараметр("Дата", НачалоМесяца(ДатаЗакрытия));

	Выборка = _Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
	Иначе
		ДокументОбъект = Документы.ЗакрытиеМесяца.СоздатьДокумент();
	КонецЕсли;

	ДокументОбъект.Дата = ДатаЗакрытия;

	ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
	ДокументСсылка = ДокументОбъект.Ссылка;

	НаборЗаписей = РегистрыСведений.СтоимостьТоваров.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ДокументЗакрытияМесяца.Установить(ДокументСсылка);

	Пока ВыборкаСебестоимость.Следующий() Цикл
		Запись = НаборЗаписей.Добавить();
		Запись.ДокументЗакрытияМесяца = ДокументСсылка;
		Запись.Номенклатура = ВыборкаСебестоимость.Номенклатура;
		Запись.Дата = ВыборкаСебестоимость.ДатаПартии;
		Запись.Себестоимость = ВыборкаСебестоимость.Цена;
	КонецЦикла;

	НаборЗаписей.Записать();

	Константы.КрайняяДатаЗакрытогоПериода.Установить(ДатаЗакрытия);
КонецПроцедуры

Функция ПолучитьСтатусЗакрытияМесяца(Дата) Экспорт
	_Запрос = Новый Запрос;
	_Запрос.Текст =
		"ВЫБРАТЬ
		|	ЕСТЬNULL(МИНИМУМ(ПартииТоваровОбороты.Период), ДАТАВРЕМЯ(1, 1, 1)) КАК Период
		|ИЗ
		|	РегистрНакопления.ПартииТоваров.Обороты(, , Месяц, ) КАК ПартииТоваровОбороты
		|ГДЕ
		|	ПартииТоваровОбороты.КоличествоПриход > 0";

	Выборка = _Запрос.Выполнить().Выбрать();
	Выборка.Следующий();

	Если Дата < Выборка.Период Тогда
		Возврат Истина;
	КонецЕсли;

	_Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗакрытиеМесяца.Ссылка
		|ИЗ
		|	Документ.ЗакрытиеМесяца КАК ЗакрытиеМесяца
		|ГДЕ
		|	НАЧАЛОПЕРИОДА(ЗакрытиеМесяца.Дата, МЕСЯЦ) = &Дата
		|	И ЗакрытиеМесяца.Проведен";

	_Запрос.УстановитьПараметр("Дата", НачалоМесяца(Дата));

	Выборка = _Запрос.Выполнить().Выбрать();

	Возврат Выборка.Следующий();
КонецФункции
